#!/usr/bin/env perl

use strict;
use warnings;

use FXC;
use FXC::Converter;
use Docopt;

my $opts = docopt(version => "$0 v$FXC::VERSION");

my $converter = FXC::Converter->new({
  cache => "$ENV{HOME}/.fx-converter-cache.xml",
  on_message => sub {
    my ($msg, $exit) = @_;

    die "$msg\n" if($exit);
    print "$msg\n";
  },
});

if($opts->{'symbols'}) {
  print join("\n", sort(@{$converter->symbols}));
  print "\n";
  exit(0);
}

sub graceful_exit {
  Docopt::Exceptions::DocoptExit->throw(shift);
}

sub validate {
  graceful_exit("Amount is not a number") unless($opts->{'<amount>'} =~ /^\d+(?:\.\d+)?$/);

  for($opts->{'<from>'}, $opts->{'<to>'}) {
    $_ = uc($_);
    graceful_exit("$_ is not a supported currency symbol") unless($converter->has_symbol($_));
  }

  return ($opts->{'<amount>'}, $opts->{'<from>'}, $opts->{'<to>'});
}


my ($amount, $from, $to) = validate;

printf("%.02f\n", $converter->convert($amount, $from, $to));


=head1 SYNOPSIS

  fx <amount> <from> in <to>
  fx <amount> <from> <to>
  fx symbols
  fx [options]

=head2 OPTIONS

  --help      Usage manual
  --version   Shows version number and exists

=head2 OPTION DETAIL

=over

=item B<<amount>>

The amount to convert from. Decimals denoted by C<.>

=item B<<from>>

ISO 4217 currency code to convert C<amount> I<from>.

=item B<<to>>

ISO 4217 currency code to convert C<amount> I<to>.

=item B<symbols>

List currency symbols available to this program.

=item B<--help>

Shows this help message.

=item B<--man>

Shows help in manual format

=item B<--version>

Shows version number and exists.

=back

=head1 DESCRIPTION

fx converts a sum from one currency to another using the European Central Banks
reference data set. This dataset is published daily at around 16.00 CET Monday
to Friday. For more information, please see ECB's website at
L<http://www.ecb.europa.eu/stats/policy_and_exchange_rates/euro_reference_exchange_rates/html/index.en.html>

The data feed is available as XML at L<http://www.ecb.europa.eu/stats/eurofxref/eurofxref-daily.xml>.

=head1 EXTERNAL DEPENDENCIES

=over

=item zlib

=item libxml2

=back

=head1 AUTHOR

Gunnar Hansson <gunnar@ghansson.com>

=cut

